#see also:  ecdumpImpl.defs
sub execEcdump
#execute the ec dump command.
#by the time we get here, all arguments are parsed, checked, and stored in my config() object,
#the database connectivity is checked, and we are ready to run.
#returns 0 on success, non-zero othewise.
{

    my ($self) = @_;

    if (!$self->haveListCommand() && !$self->haveDumpCommand()) {
        printf STDERR "%s:  nothing to do - please specify a list or dump command.\n", $self->progName();
        return 0;
    }

    #output directory is defined only if we are dumping:
    if ($self->haveDumpCommand()) {
        if ($self->doClean()) {
            if ($self->cleanOutputDir() != 0) {
                printf STDERR "%s: ERROR: clean output directory step failed. ABORT\n", $self->progName();
                return 1;
            }
        }

        #now create output directory:
        if ($self->createOutputDir() != 0) {
            printf STDERR "%s: ERROR: create output directory step failed. ABORT\n", $self->progName();
            return 1;
        }
    }

    #initialize db connection if yet not done:
    if (!$self->getDbConnectionInitialized()) {
        if ($self->initDbConnection() != 0) {
            printf STDERR "%s: ERROR: cannot get a database connection. ABORT\n", $self->progName();
            return 1;
        }
    }

    #cache handle for EcProjects object:
    my $ecprojects = $self->ecProjects();

    my $nerrs = 0;

    if ($self->haveListCommand() || $self->dumpAllProjects()) {
        $nerrs += $ecprojects->addAllProjects();
    } else {
        for my $pjname ($self->projectList()) {
            $nerrs += $ecprojects->addOneProject($pjname);
        }
    }

    if ($nerrs) {
        printf STDERR "%s:  encounterd %d ERROR%s while adding projects. ABORT\n", $self->progName(), $nerrs, ($nerrs == 1 ? '' : 'S');
        return 1;
    }

    if ($self->haveListCommand()) {
        return $self->processListCommand();
    }
    
    #otherwise, we have a dump command:
    return $self->processDumpCommand();
}

sub processDumpCommand
#process dump command.  return 0 on success.
{
    my ($self) = @_;

    printf STDERR "DUMPING EC CONTENT -> %s\n", $self->rootDir if $VERBOSE;

    my $ecprojects = $self->ecProjects();
    #tell ecProjects to load data:
    if ($ecprojects->loadProjects() != 0) {
        printf STDERR "%s: ERROR: load data step failed, aborting dump.\n", ::srline();
        return 1;
    }

    #tell ecProjects to dump data into output root:
    return $ecprojects->dumpProjects(0);
}

sub processListCommand
#process list command.  return 0 on success.
{
    my ($self) = @_;
    my $ecprojects = $self->ecProjects();

    #tell ecProjects to list itself:
    return $ecprojects->listProjectNames();
}

sub initDbConnection
#initialize the database connection and execute the command.
{
    my ($self) = @_;
    my $sqlpj = $self->sqlpj();

    if (!$sqlpj->sql_init_connection()) {
        printf STDERR "%s: ERROR:  failed to get a database connection.\n", ::srline();
        return 1;
    }

    #tell sqlpj to always make results available via getQueryResult() instead of displaying on stdout:
    $sqlpj->setOutputToList(1);

    $self->setDbConnectionInitialized(1);
    return 0;
}

sub cleanOutputDir
#remove the output directory.  return 0 if successful.
{
    my ($self) = @_;
    my $outroot = $self->rootDir();

    printf STDERR "Deleting output dir '%s'...\n", $outroot if $VERBOSE;

    if (-d $outroot) {
        os::rm_recursive($outroot);
        if (-d $outroot) {
            printf STDERR "%s:  ERROR: can't remove output dir, '%s'\n", ::srline, $outroot;
            return 1;
        }
    } elsif (-e $outroot) {
        #plain file or symlink, use less firepower:
        os::rmFile($outroot);
        if (-e $outroot) {
            printf STDERR "%s:  ERROR: can't remove '%s'\n", ::srline, $outroot;
            return 1;
        }
    }

    return 0;
}

sub createOutputDir
#create the output directory.  return 0 if successful.
{
    my ($self) = @_;
    my $outroot = $self->rootDir();

    printf STDERR "Creating output dir '%s'...\n", $outroot if $VERBOSE;

    os::createdir($outroot, 0775) if (! -d $outroot);
    if (!-d $outroot) {
        printf STDERR "%s: can't create output dir, '%s' (%s)\n", ::srline, $outroot, $!;
        return 1;
    }
    return 0;
}

