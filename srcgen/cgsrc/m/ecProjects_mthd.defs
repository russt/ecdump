#see also:  ecProjects.defs
sub loadDumpProjects
#load and dump projects one at a time instead of all at once.
#this allows us to garbage collect between projects.
{
    my ($self, $indent) = @_;
    my $outroot = $self->rootDir();

    return 1 unless ($self->createOutdir() == 0);

    printf STDERR "%sDUMPING PROJECTS -> %s\n", ' 'x$indent, $outroot if ($VERBOSE);

    $self->loadEcTopLevel();
    $self->dumpEcTopLevel();

    my $errs = 0;
    for my $pj ($self->ecProjects()) {
        if ($pj->loadProject() != 0) {
            printf STDERR "%s: ERROR:  failed to load project '%s'\n", ::srline(), $pj->projectName;
            ++$errs;
        } else {
            if ($pj->dumpProject($indent+2) != 0) {
                printf STDERR "%s: ERROR:  failed to dump project '%s'\n", ::srline(), $pj->projectName;
                ++$errs;
            }
        }
    }

    return $errs;
}

sub loadProjects
#load each project from the database
{
    my ($self) = @_;

    $self->loadEcTopLevel();

    for my $pj ($self->ecProjects()) {
        $pj->loadProject();
    }

    return 0;
}

sub createOutdir
{
    my ($self) = @_;
    my $outroot = $self->rootDir();

    os::createdir($outroot, 0775) unless (-d $outroot);
    if (!-d $outroot) {
        printf STDERR "%s: can't create output dir, '%s' (%s)\n", ::srline(), $outroot, $!;
        return 1;
    }

    return 0;
}

sub dumpProjects
#dump each project to the dump tree.
{
    my ($self, $indent) = @_;
    my $outroot = $self->rootDir();

    printf STDERR "%sDUMPING PROJECTS -> %s\n", ' 'x$indent, $outroot if ($VERBOSE);

    return 1 unless ($self->createOutdir() != 0);

    $self->dumpEcTopLevel();

    my $errs = 0;
    for my $pj ($self->ecProjects()) {
        $errs += $pj->dumpProject($indent+2);
    }

    return $errs;
}

sub listProjectNames
#display the current list of project names
{
    my ($self) = @_;

    for my $pj ($self->ecProjects()) {
        printf "%s\n", $pj->projectName();
    }
}

sub loadEcTopLevel
#load top-level EC metadata
{
    #TBD.
    return 0;
}

sub dumpEcTopLevel
#dump top-level EC metadata
{
    #TBD.
    return 0;
}

sub addOneProject
#supports list and dump commands.
#can be called from outside (to process a list of user-supplied projects).
#add a single project to the collection.
#does not fully populate sub-objects. for that, use loadProjects();
#return 0 on success.
{
    my ($self, $projectName) = @_;

    #initialize project keys if not done yet:
    return 1 unless ($self->getDbKeysInitialized() || !$self->initDbKeys());

    #check that we have a legitimate project name:
    if (!defined($self->getNameIdMap->{$projectName})) {
        printf STDERR "%s:  ERROR:  project '%s' is not in the database.\n", ::srline(), $projectName;
        return 1;
    }

    #no setter, for mEcProjects - so use direct ref:
    push @{$self->{'mEcProjects'}},
        (new {=_ECPROJECT_PACKAGE=}($self, $projectName, $self->getNameIdMap->{$projectName}, $self->getNamePropIdMap->{$projectName}));

    #TODO:  add project-level properties

    return 0;
}

sub addAllProjects
#add all of the EC projects to the collection.
#returns 0 on success
{
    my ($self) = @_;

    #initialize project keys if not done yet:
    return 1 unless ($self->getDbKeysInitialized() || !$self->initDbKeys());

    #make sure we start with a clean list, in the event this routine has already been called:
    $self->{'mEcProjects'} = [];

    #now add one project obj. per retrieved project:
    for my $name (sort keys %{$self->getNameIdMap()}) {
        $self->addOneProject($name);
    }

    return 0;
}

sub initDbKeys
#initialize project keys.  This only needs to happen once.
#if okay, then we set DbKeysInitialized attribute to true.
#return 0 on success.
{
    my ($self) = @_;
    my ($sqlpj) = $self->sqlpj();

    my $lbuf = "select name,id,property_sheet_id from ec_project";

    printf STDERR "%s: running sql query to get project keys\n", ::srline() if ($DDEBUG);

    if ( !$sqlpj->sql_exec($lbuf) ) {
        printf STDERR "%s:  ERROR:  query '%s' failed.\n", ::srline(), $lbuf;
        return 1;
    }

    #o'wise, stash results (query returns a ref to a list of list refs):
    my @results = map {
        @{$_};    #dereference each row.  we expect (name,id,propId) triples.
    } @{$sqlpj->getQueryResult()};


    #map (name,id,propert_sheet_id) triples into nameId and namePropId hashes:
    my (%nameId, %namePropId);
    for (my $ii=0; $ii < $#results; $ii += 3) {
        $nameId{$results[$ii]} = $results[$ii+1];
        $namePropId{$results[$ii]} = $results[$ii+2];
    }
    
    $self->setNameIdMap(\%nameId);
    $self->setNamePropIdMap(\%namePropId);

    if ($DDEBUG) {
        printf STDERR "%s: nameId result=\n", ::srline();
        $utils->dumpDbKeys(\%nameId);

        printf STDERR "%s: namePropId result=\n", ::srline();
        $utils->dumpDbKeys(\%namePropId);
    }

    $self->setDbKeysInitialized(1);
    return 0;
}

sub fetchDescription
#pull the description for table <table>
#caller must have a setDescription(string) method.
#return 0 if successful
{
    my ($self, $table, $id) = @_;
    my ($sqlpj) = $self->sqlpj();

    #this is a result:
    $self->setDescription('');

    #this query should return only one row:
    my $lbuf = sprintf("select description, description_clob_id from %s where id=%d", $table, $id) ;

    printf STDERR "%s: running sql query to get description field\n", ::srline() if ($DDEBUG);

    if ( !$sqlpj->sql_exec($lbuf) ) {
        printf STDERR "%s:  ERROR:  query '%s' failed.\n", ::srline(), $lbuf;
        return 1;
    }

    #o'wise, stash results (query returns a ref to a list of list refs):
    my @results = map {
        @{$_};    #dereference each row.  we expect one row with (description,description_clob_id) pair
    } @{$sqlpj->getQueryResult()};

    if ( $#results+1 != 2 ) {
        printf STDERR "%s:  ERROR:  query '%s' returned wrong number of results (%d).\n", ::srline(), $lbuf, $#results+1;
        return 1;
    }

    my ($descStr, $descClobId) = ($results[0], $results[1]);

    $descStr    = '' unless (defined($descStr));
    $descClobId = '' unless (defined($descClobId));

    printf STDERR "%s: (descStr,descClobId)=(%s,%s)\n", ::srline(), $descStr, $descClobId if ($DDEBUG);

    #Note:  if we have a string and a clob, we prefer the clob, which is the full content

    if ($descClobId ne '') {
        my $clobtxt = '';
        if ($self->fetchClobText(\$clobtxt, $descClobId) != 0) {
            printf STDERR "%s:  ERROR:  failed to fetch description clob='%s' for %s[%s]\n", ::srline(), $descClobId, $table, $id;
            return 1;
        }
        $self->setDescription($clobtxt);
    } elsif ($descStr ne '') {
        $self->setDescription($descStr);
    }

    return 0;
}

sub fetchClobText
{
    my ($self, $txtref, $id) = @_;
    my ($sqlpj) = $self->sqlpj();

    #this is a result:
    $$txtref = '';

    #this query should return only one row:
    my $lbuf = sprintf("select clob from ec_clob where id=%d", $id);

    printf STDERR "%s: running sql query to get clob\n", ::srline() if ($DDEBUG);

    if ( !$sqlpj->sql_exec($lbuf) ) {
        printf STDERR "%s:  ERROR:  query '%s' failed.\n", ::srline(), $lbuf;
        return 1;
    }

    #o'wise, stash results (query returns a ref to a list of list refs):
    my @results = map {
        @{$_};    #dereference each row.  we expect one row containing the clob
    } @{$sqlpj->getQueryResult()};

    if ( $#results+1 != 1 ) {
        printf STDERR "%s:  ERROR:  query '%s' returned wrong number of results (%d).\n", ::srline(), $lbuf, $#results+1;
        return 1;
    }

    #otherwise we found the clob, set the result:
    $$txtref = $results[0];

    return 0;
}

sub dumpDescription
#write the description out.
#caller must have a getDescription(), rootDir  methods.
#return 0 if successful
{
    my ($self) = @_;
    my $txt = $self->getDescription();

    #don't create empty files:
    return if ($txt eq '');

    my $outroot = $self->rootDir();

    #fix eol:
    $txt = "$txt\n" unless ($txt eq '' || $txt =~ /\n$/);

    return os::write_str2file(\$txt, path::mkpathname($outroot, "description"));
}
