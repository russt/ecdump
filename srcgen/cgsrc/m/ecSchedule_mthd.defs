#see also:  ecSchedule.defs
sub loadSchedule
#load this schedule.
{
    my ($self) = @_;
    my($name, $id) = ($self->scheduleName(), $self->scheduleId());
    my $outroot = $self->rootDir();

    #first load this schedule:
    printf STDERR "LOADING SCHEDULE (%s,%s)\n", $name, $id  if ($DDEBUG);

    #get my description (method defined in ecProjects):
    $self->fetchDescription('ec_schedule', $id);

    #get my content:
    $self->fetchSchedContent($name, $id);

    #load my actual parameters:
    $self->ecActualParameters->loadActualParameters();

    #load my properties:
    $self->ecProps->loadProps();

    return 0;
}

sub dumpSchedule
#dump this schedule.
#return 0 on success
{
    my ($self, $indent) = @_;
    my $outroot = $self->rootDir();

    #first dump this schedule:
    printf STDERR "%sDUMPING SCHEDULE (%s,%s) -> %s\n", ' 'x$indent, $self->scheduleName, $self->scheduleId, $outroot if ($DEBUG);

    os::createdir($outroot, 0775) unless (-d $outroot);
    if (!-d $outroot) {
        printf STDERR "%s: can't create output dir, '%s' (%s)\n", ::srline(), $outroot, $!;
        return 1;
    }

    #write my description out:
    $self->dumpDescription();

    #write my content out:
    $self->dumpSchedContent();

    #dump my actual parameters:
    $self->ecActualParameters->dumpActualParameters();

    #dump my properties:
    $self->ecProps->dumpProps($indent+2);

    return 0;
}

sub dumpSchedContent
#write the schedule content to files.
#return 0 if successful
{
    my ($self) = @_;
    my ($errs) = 0;

    $errs += $self->dumpSchedSwitches();
    $errs += $self->dumpSchedProcedure();

    return $errs;
}

sub dumpSchedSwitches
#write the misc. schedule properties
#return 0 if successful
{
    my ($self) = @_;
    my $txt = $self->getSwitchText();
    my $contentfn = $self->switchTextFname();

    #don't create empty files:
    return 0 if ($txt eq '');

    my $outroot = $self->rootDir();

    #fix eol:
    $txt = "$txt\n" unless ($txt eq '' || $txt =~ /\n$/);

    return os::write_str2file(\$txt, path::mkpathname($outroot, $contentfn));
}

sub dumpSchedProcedure
#write the misc. schedule procedure content
#return 0 if successful
{
    my ($self) = @_;
    my $contentfn = $self->schedProcedureFname();
    my $procedure = $self->getSchedProcedure();
    my $project = $self->projectName();
    my $txt = $procedure;

    #don't create empty files:
    return 0 if ($txt eq '');

    my $outroot = $self->rootDir();

    if ($procedure ne '' && $project ne '') {
        $txt = sprintf("%s/%s", $project, $procedure);
    }

    #fix eol:
    $txt = "$txt\n" unless ($txt eq '' || $txt =~ /\n$/);

    return os::write_str2file(\$txt, path::mkpathname($outroot, $contentfn));
}

sub fetchSchedContent
#fetch the schedule content for a given schedule id.
#return 0 if successful
{
    my ($self, $name, $id) = @_;
    my ($sqlpj) = $self->sqlpj();

    ###########
    #Attributes of interest in ec_schedule
    #    id, version, name, begin_date, description, disabled, end_date,
    #    run_interval, interval_units, misfire_policy, month_days,
    #    priority, procedure_name, start_time, stop_time, time_zone, week_days,
    #    acl_id, property_sheet_id, actual_parameters_id, description_clob_id, project_id
    ###########

    #this query should return only one row:
    my $lbuf = sprintf("select project_id,procedure_name,version,begin_date,disabled,end_date,run_interval,interval_units,misfire_policy,month_days,priority,start_time,stop_time,time_zone,week_days from ec_schedule where id=%d", $id);

    printf STDERR "%s: running sql query to get schedule content fields\n", ::srline() if ($DDEBUG);

    if ( !$sqlpj->sql_exec($lbuf) ) {
        printf STDERR "%s:  ERROR:  query '%s' failed.\n", ::srline(), $lbuf;
        return 1;
    }

    #o'wise, stash results (query returns a ref to a list of list refs):
    my @results = map {
        @{$_};    #dereference each row.  we expect one row
    } @{$sqlpj->getQueryResult()};

    if ( $#results+1 != 15 ) {
        printf STDERR "%s:  ERROR:  query '%s' returned wrong number of results (%d).\n", ::srline(), $lbuf, $#results+1;
        return 1;
    }

    #map undefined values:
    @results = map {
        defined($_) ? $_ : '';
    } @results;

    my ($project_id,$procedure_name,$version,$begin_date,$disabled,$end_date,$run_interval,$interval_units,$misfire_policy,$month_days,$priority,$start_time,$stop_time,$time_zone,$week_days) = @results;

    #$self->setDDebug(1);
    printf STDERR "%s: (project_id,procedure_name,version,begin_date,disabled,end_date,run_interval,interval_units,misfire_policy,month_days,priority,start_time,stop_time,time_zone,week_days)=(%s)\n", ::srline(), join(',', (@results)) if ($DDEBUG);
    #$self->setDDebug(0);

    $self->setSchedProcedure($procedure_name);

    #create switches text (in a form that can be consumed by a posix shell):
    my $switches = sprintf("sched_begin_date='%s'
sched_disabled='%s'
sched_end_date='%s'
sched_run_interval='%s'
sched_interval_units='%s'
sched_misfire_policy='%s'
sched_month_days='%s'
sched_priority='%s'
sched_start_time='%s'
sched_stop_time='%s'
sched_time_zone='%s'
sched_week_days='%s'
sched_version='%s'", $begin_date,$disabled,$end_date,$run_interval,$interval_units,$misfire_policy,$month_days,$priority,$start_time,$stop_time,$time_zone,$week_days,$version);

    $self->setSwitchText($switches);

    return 0;
}
